library HeroUtils requires TimerUtils
    globals
        constant string deathMessageEarly       = "You lost |c0061FFFF10 000|r golds and will revive after |c0061FFFF2|r minutes .
Command |c0061FFFF-revive|r allows you to revive before the cooldown at the cost of |c0061FFFF5000|r gold."
        constant string deathMessageLate        = "You lost |c0061FFFF100 000|r golds and will revive after |c0061FFFF2|r minutes .
Command |c0061FFFF-revive|r allows you to revive before the cooldown at the cost of |c0061FFFF50|r lumber."
        constant string reviveSuccessEarly      = "|c00c08507The gods have accepted your offering, your hero has been revived.|r"
        constant string reviveNotEnoughEarly    = "|c00640000Not enough gold. You need 10 000 gold to revive your hero early|r"
        constant string reviveSuccessLate       = "|c00c08507The gods have accepted your offering, your hero has been revived.|r"
        constant string reviveNotEnoughLate     = "|c00640000Not enough lumber. You need 50 lumber to revive your hero early|r"

        constant real timerDeathEarly           = 120.0
        constant real timerDeathLate            = 45.0

        timer array deathTimer
        timerdialog array deathTimerDialog

        boolean isLateGame                      = false
    endglobals

    function HeroRevive takes integer pId returns nothing
        local timer t                   = deathTimer[pId]
        local unit hero                 = udg_LightHeroes[pId]
        
	    if pId <= 0 then
            call DebugLog(LVL_WARN, "HeroRevive FAILED: pId is invalid (" + I2S(pId) + ")")
            return
        endif

        if t != null then
            call DestroyTimerDialog(deathTimerDialog[pId])
            call ReleaseTimer(t)
            

            set deathTimer[pId] = null
            set deathTimerDialog[pId] = null
            set t = null
        endif
        
        call ReviveHeroLoc(hero, udg_StartPoint, true)
        
        if IsTriggerEnabled(gg_trg_DualHeroChange) == true then
            set udg_DualHeroChange[pId] = true
        endif
    endfunction

    function HeroCommandRevive takes nothing returns nothing
        local player p              = GetTriggerPlayer()
        local force playerForce     = CreateForce()
        local integer pId           = GetConvertedPlayerId(p)
        
        local integer playerGold    = GetPlayerState(p, PLAYER_STATE_RESOURCE_GOLD)
        local integer playerLumber  = GetPlayerState(p, PLAYER_STATE_RESOURCE_LUMBER)

        call ForceAddPlayer(playerForce, p)

        if isLateGame then
            if playerLumber >= 50.0 then
                call AdjustPlayerStateBJ(-50, p, PLAYER_STATE_RESOURCE_LUMBER)
                call HeroRevive(pId)
                call DisplayTimedTextToForce(playerForce, 10.0, reviveSuccessLate)
            else
                call DisplayTimedTextToForce(playerForce, 10.0, reviveNotEnoughLate)
            endif
        else 
            if playerGold >= 10000.0 then
                call AdjustPlayerStateBJ(-10000, p, PLAYER_STATE_RESOURCE_GOLD)
                call HeroRevive(pId)
                call DisplayTimedTextToForce(playerForce, 10.0, reviveSuccessEarly)
            else
                call DisplayTimedTextToForce(playerForce, 10.0, reviveNotEnoughEarly)
            endif
        endif

        call DestroyForce(playerForce)
    endfunction

    function HeroTimerRevive takes nothing returns nothing
        local timer t                   = GetExpiredTimer()
        local integer pId               = GetTimerData(t)

        if pId <= 0 then
            call DebugLog(LVL_WARN, "HeroRevive FAILED: Invalid pId (" + I2S(pId) + ")")
            return 
        endif
        
        call HeroRevive(pId)
    endfunction

    function HeroDeath takes nothing returns nothing 
        local unit hero             = GetTriggerUnit()
        local player heroPlayer     = GetOwningPlayer(hero)
        local force playerForce     = CreateForce()
        local integer pId           = GetConvertedPlayerId(heroPlayer)

        local timer t               = NewTimer()
        local real wait

        call DebugLog(LVL_DEBUG, "HeroDead - Player " + GetPlayerName(heroPlayer) )

	call SetTimerData(t, pId)
	
	set deathTimer[pId] = t
	call ForceAddPlayer(playerForce, heroPlayer)     
        

        if IsTriggerEnabled(gg_trg_DualHeroChange) == true then
            set udg_DualHeroChange[pId] = false
        endif

        if isLateGame then
            call DisplayTimedTextToForce(playerForce, 10.00, deathMessageLate)
            set wait = timerDeathLate
        else 
            call DisplayTimedTextToForce(playerForce, 10.00, deathMessageEarly)
            set wait = timerDeathEarly
            call AdjustPlayerStateBJ(-10000, heroPlayer, PLAYER_STATE_RESOURCE_GOLD)
        endif

        call TimerStart(t, wait, false, function HeroTimerRevive)
        set deathTimerDialog[pId] = CreateTimerDialogBJ(t, TruncatedPlayerName(heroPlayer) + " Resurrection:" )

        call DestroyForce(playerForce)
    endfunction

    function HeroReviveEnterLateGame takes nothing returns nothing
        set isLateGame = true
    endfunction
endlibrary